on join:
	send "", "&8▀▄▀▄▀▄  &a&l  ᴘᴀʀᴛʏɢɢ  &8   ▄▀▄▀▄▀", "", "&7 &7 &7 &7 &7 &7 &7 &7 &7 &7Designed by &9Santio71" and "" to player
	while player is online:
		set {_name} to {displayname::%player%} ? "%player%"
		set {_rank} to getRank({_name} parsed as offlineplayer)
		set {_prefix} to getPrefix({_rank})
		set {_suffix} to getSuffix({_rank})
		set {_color} to getRankColor({_rank})
		set player's display name to colored "%{_prefix}%%{_name}%"
		set player's tab list name to "%colored {_color}%%{_name}%"
		set tablist header for player to "", "&3&lPartyGG" and ""
		set tablist footer for player to "" and "&7%getNews()%"
		wait a tick

on chat:
	set {_name} to {displayname::%player%} ? "%player%"
	set {_rank} to getRank({_name} parsed as offlineplayer)
	set {_color} to first 2 characters of getSuffix({_rank})

	set chat format to "%player's display name%%{_color}%: %message%"
	set chat format to colored "%player's display name%%{_color}%: %message%" if player has permission "chat.color"

on join:
	if {banned::%uuid of player%} is set:
		set {_id} to {banned::%uuid of player%}
		set {_time} to {punishments::%{_id}%::time} 
		set {_diff} to difference between now and {_time}
		if {_diff} >= {punishments::%{_id}%::length}:
			clear {banned::%uuid of player%}
			set join message to "&7%player's display name% &7joined."
			send "", "&a&lBAN EXPIRED!", "&7Your ban has expired and you are now able to play!" and ""
		else:
			set join message to ""
			wait a tick
			kick player due to "%nl%&6&lBAN%nl%&7You are banned for &e%{bans::%{_id}%::reason}%&7!%nl%"
	else:
		set join message to "&7%player's display name% &7joined."

on quit:
	set quit message to "&7%player's display name% &7left."

command /rank [<text>] [<text>] [<text>]:
	permission: rank.set
	permission message: Unknown command. Type "/help" for help.
	trigger:
		if arg-1 is not set:
			send "&c/rank <player> <rank>"
			send "&c/rank [prefix|suffix] <rank> <value>"
			stop
		else if arg-1 is "prefix" or "suffix":
			if arg-3 is not set:
				send "&c/rank %arg-1% <group> <string>"
				stop
			set {rankData::%arg-2%::%arg-1%} to colored arg-3
			send "&3&lRANK &7Successfully set the %arg-1%&7 of &3%arg-2%&7 to &3%arg-3%&7!"
		else:
			if arg-2 is not set:
				send "&c/rank <player> <rank>"
				stop
			
			set {_p} to arg-1 parsed as offlineplayer
			set {_u} to uuid of {_p}
			
			set {rank::%{_u}%} to arg-2
			set {_color} to getRankColor(arg-2)
			set {_rank} to capitalized arg-2
			
			send "&3&lRANK &7Successfully set the rank of &3%arg-1% &7to &3%arg-2%&7!"
			send colored "&7Your rank was set to %{_color}%%uncolored {_rank}%&7!" to {_p}

function getNews() :: text:
	return {partygg::news} ? ("&7PartyGG.minehut.gg")

function getRank(p: offlineplayer) :: text:
	set {_u} to uuid of {_p}
	return {rank::%{_u}%} ? ("Default")

function getPrefix(r: text) :: text:
	return colored {rankData::%{_r}%::prefix} ? ("&7")

function getSuffix(r: text) :: text:
	return colored {rankData::%{_r}%::suffix} ? ("&7")
	
function getRankColor(r: text) :: text:
	return first 2 characters of getPrefix({_r})
	
function getWeight(p: offlineplayer) :: integer:
	set {_rank} to getRank({_p})

	if {_rank} contains "Default":
		return 1
	else if {_rank} contains "Build":
		return 2
	else if {_rank} contains "Staff":
		return 3
	else if {_rank} contains "Admin":
		return 4
	else if {_rank} contains "Owner":
		return 5
